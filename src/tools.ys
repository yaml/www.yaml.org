!ys-0

# YAML Tools Data and Generator
# This file generates docs/tools.md

defn main():
  data =: RUN.args:first
          .replace(/\.ys$/ '.yaml')
          .read():yaml/load

  markdown =: reduce-kv(category "" data)

  say: |-
    ---
    edit: src/tools.yaml
    ---

    # YAML Tools

    Tools and utilities for working with YAML - schema validators, linters, editors, and related technologies.

    <div class="badge-legend" markdown>
      [:material-heart:](https://yaml.com){ .badge .badge-tycs }
      Supported by [The YAML Company](https://yaml.com)
    </div>

    ---

    $(markdown:trim)

defn category(markdown cat tools): |
  $markdown
  ## $cat

  <div class="grid cards" markdown>

  $(reduce(section "" tools))</div>


defn section(section tool):
  name site icon desc tycs =:
    tool.slice(qw(name site icon desc tycs))

  tycs-badge =:
    when tycs:
      ' [:material-heart:](https://yaml.com "Supported by The YAML Company"){ .badge .badge-tycs }'

  section +: |+
    -   :$icon:{ .lg .middle } **[$name]($site){ target="_blank" }**$tycs-badge

        ---

        $desc
