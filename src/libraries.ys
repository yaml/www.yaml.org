!ys-0

# YAML Libraries Data and Generator
# This file generates docs/libraries.md

defn main():
  data =: RUN.args:first
          .replace(/\.ys$/ '.yaml')
          .read():yaml/load

  markdown =: reduce-kv(language "" data)

  say: |-
    # YAML Libraries

    YAML data serialization libraries for most modern programming languages.

    <div class="badge-legend" markdown>
      [:material-test-tube:](
      https://github.com/yaml/yaml-test-suite){ .badge .badge-yts }
      Uses YAML Test Suite<br>
      [:material-heart:](https://yaml.com){ .badge .badge-tycs }
      Supported by The YAML Company
    </div>

    ---

    $(markdown:trim)

defn language(markdown lang yamls): |
  $markdown
  ## $lang

  <div class="grid cards" markdown>

  $(reduce(section "" yamls))</div>


defn section(section yaml):
  name site icon desc yts tycs =:
    yaml.slice(qw(name site icon desc yts tycs))

  yts-badge =:
    when yts:
      " [:material-test-tube:](\n
      https://github.com/yaml/yaml-test-suite
      \"Uses YAML Test Suite\"){ .badge .badge-yts }"

  tycs-badge =:
    when tycs:
      " [:material-heart:](\n
      https://yaml.com \"Supported by The YAML Company\")\n
      { .badge .badge-tycs }"

  section +: |+
    -   :$icon:{ .lg .middle } **[$name]($site)**$tycs-badge$yts-badge

        ---

        $desc
