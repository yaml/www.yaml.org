!ys-0

# YAML Libraries Data and Generator
# This file generates docs/libraries.md

defn main():
  data =: RUN.args:first
          .replace(/\.ys$/ '.yaml')
          .read():yaml/load

  markdown =: reduce-kv(language "" data)

  say: |-
    # YAML Libraries

    YAML data serialization libraries for most modern programming languages.

    <div class="badge-legend" markdown>
      [:material-test-tube:](
      https://github.com/yaml/yaml-test-suite){ .badge .badge-yts target="_blank" }
      Uses the [YAML Test Suite](https://github.com/yaml/yaml-test-suite){ target="_blank" }<br>
      [:material-heart:](https://yaml.com){ .badge .badge-tycs target="_blank" }
      Supported by [The YAML Company](https://yaml.com){ target="_blank" }<br>
      [:material-play-circle:](https://play.yaml.com){ .badge .badge-play target="_blank" }
      Available in the [YAML Playground](https://play.yaml.com){ target="_blank" }
    </div>

    ---

    $(markdown:trim)

defn language(markdown lang yamls): |
  $markdown
  ## $lang

  <div class="grid cards" markdown>

  $(reduce(section "" yamls))</div>


defn section(section yaml):
  name site icon desc yts tycs play =:
    yaml.slice(qw(name site icon desc yts tycs play))

  yts-badge =:
    when yts:
      ' [:material-test-tube:](https://github.com/yaml/yaml-test-suite "Uses YAML Test Suite"){ .badge .badge-yts target="_blank" }'

  tycs-badge =:
    when tycs:
      ' [:material-heart:](https://yaml.com "Supported by The YAML Company"){ .badge .badge-tycs target="_blank" }'

  play-badge =:
    when play:
      " [:material-play-circle:](https://play.yaml.com?show=$play \"Available in the YAML Playground\"){ .badge .badge-play target=\"_blank\" }"

  section +: |+
    -   :$icon:{ .lg .middle } **[$name]($site){ target="_blank" }**$play-badge$tycs-badge$yts-badge

        ---

        $desc
