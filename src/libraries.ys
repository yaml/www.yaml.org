!ys-0

# YAML Libraries Data and Generator
# This file generates docs/libraries.md

defn main():
  data =: RUN.args:first
          .replace(/\.ys$/ '.yaml')
          .read():yaml/load

  markdown =: reduce-kv(language "" data)

  say: |-
    # YAML Libraries

    YAML data serialization libraries for most modern programming languages.

    <div class="badge-legend" markdown>
      [:material-test-tube:](
      https://github.com/yaml/yaml-test-suite){ .badge .badge-yts }
      Uses the [YAML Test Suite](https://github.com/yaml/yaml-test-suite)<br>
      [:material-heart:](https://yaml.com){ .badge .badge-tycs }
      Supported by [The YAML Company](https://yaml.com)<br>
      [:material-play-circle:](https://play.yaml.com){ .badge .badge-play }
      Available in the [YAML Playground](https://play.yaml.com)
    </div>

    ---

    $(markdown:trim)

defn language(markdown lang yamls): |
  $markdown
  ## $lang

  <div class="grid cards" markdown>

  $(reduce(section "" yamls))</div>


defn section(section yaml):
  name site icon desc yts tycs play =:
    yaml.slice(qw(name site icon desc yts tycs play))

  yts-badge =:
    when yts:
      ' [:material-test-tube:](https://github.com/yaml/yaml-test-suite "Uses YAML Test Suite"){ .badge .badge-yts }'

  tycs-badge =:
    when tycs:
      ' [:material-heart:](https://yaml.com "Supported by The YAML Company"){ .badge .badge-tycs }'

  play-badge =:
    when play:
      ' [:material-play-circle:](https://play.yaml.com "Available in YAML Playground"){ .badge .badge-play }'

  section +: |+
    -   :$icon:{ .lg .middle } **[$name]($site)**$play-badge$tycs-badge$yts-badge

        ---

        $desc
